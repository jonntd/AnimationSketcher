
global proc BindSelected3dCharacter()
{
	// currently simply bind a geometry
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		
		textField -e -text $selected[0] tf_3dc;
    }
}

global proc BindSelectTrajectory()
{
	// bind a NURB curve (projected?)
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		
        textField -e -text $selected[0] tf_trajectory;
    }
}

global proc BindSelectedJoint()
{
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		
        textField -e -text $selected[0] tf_rootJoint;
    }
}


//----- Bind LOA ----------

// return: curveIntersect Node
global proc string GetIntersectNode(string $c1, string $c2)
{
	//TODO: use direction, active view direction
	string $node = `curveIntersect $c1 $c2`;
	return $node;
}



//use `pointOnCurve` to get position


// LOA List

global proc AddSelectedLOA()
{
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		textScrollList -e -append $selected[0] -sc "OnSelectLOAListItem" list_loa;
    }
}

global proc RemoveSelectedLOA()
{
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		textScrollList -e -removeItem $selected[0] list_loa;
    }
}

global proc CleanLOAList()
{
	textScrollList -e -ra list_loa;
}


global proc OnSelectLOAListItem()
{
	//string $stextScrollList
	select -r `textScrollList -q -selectItem list_loa`;
}

//----------------------------



// deform to LOA

global proc DeformToLOA()
{
	select -r `textScrollList -q -selectItem list_loa`;

	CurveIKCmd;
}





//-----------------------------



global proc ApplyAnimation()
{
	string $character = `textField -query -text tf_3dc`;
	string $curveName = `textField -query -text tf_trajectory`;
    string $rootJointName = `textField -query -text tf_rootJoint`;
    
    string $childrenJoints[] = `listRelatives -ad $rootJointName`;
    string $leafJointName = $childrenJoints[0];
    select -r $rootJointName $leafJointName;
    string $ikHandlesNames[] = `ikHandle -sol ikSplineSolver -ns 30`;
    string $ikHandleName = $ikHandlesNames[0];
    string $ikSplineName = $ikHandlesNames[2];

    xform -cp $ikSplineName; //Center spline curve pivot

    select -r $ikHandleName;
    setAttr ($ikHandleName+".dTwistControlEnable") 1;

    //pathAnimation -startTimeU `playbackOptions -query -minTime` -endTimeU `playbackOptions -query -maxTime` -c $curveName $character;
    select -r $ikSplineName $curveName;
    pathAnimation -fractionMode true -follow true -followAxis z -upAxis y -worldUpType "vector" -worldUpVector 0 1 0 -inverseUp false -inverseFront false -bank false -startTimeU `playbackOptions -query -minTime` -endTimeU  `playbackOptions -query -maxTime`;

    flow -divisions 2 2 30 -objectCentered 1 -localCompute 0 -localDivisions 2 2 2 $ikSplineName;
}

global proc ApplyAnimationCpp()
{
    string $character = `textField -query -text tf_3dc`;
	string $curveName = `textField -query -text tf_trajectory`;
    string $rootJointName = `textField -query -text tf_rootJoint`;

    string $childrenJoints[] = `listRelatives -ad $rootJointName`;
    string $leafJointName = $childrenJoints[0];
    select -r $rootJointName $leafJointName;
    createNode ikSolverNode;
    ikHandle -sol ikSolverNode -sj $rootJointName -ee $leafJointName;
}


global proc AnimationSketcherMainMenu()
{
	if (`window -exists animationSketcherWindow`) deleteUI animationSketcherWindow;

	window -title "Animation Sketcher" -widthHeight 600 480 animationSketcherWindow;

	columnLayout -adj 1;

	rowLayout 
		-numberOfColumns 3 
		-columnWidth3 150 300 150 
		-columnAlign3 "right" "center" "center";

	text -l "3D Character" t_3dc;
	textField -ed false -w 300 tf_3dc;
	button -l "Bind Selected Character" -command "BindSelected3dCharacter" btn_3dc;

	setParent ..;

	rowLayout 
		-numberOfColumns 3 
		-columnWidth3 150 300 150 
		-columnAlign3 "right" "center" "center";

	text -l "Trajectory Curve" t_trajectory;
	textField -ed false -w 300 tf_trajectory;
	button -l "Bind Selected Curve" -command "BindSelectTrajectory" btn_trajectory;

	setParent ..;

	rowLayout 
		-numberOfColumns 3 
		-columnWidth3 150 300 150 
		-columnAlign3 "right" "center" "center";

	//TODO: this should be a table, a list of curves
	text -l "Shape Root Joint" t_rootJoint;
	textField -ed false -w 300 tf_rootJoint;
	button -l "Bind Selected Joint" -command "BindSelectedJoint" btn_rootJoint;

	setParent ..;

	//----- LOA menu list
	rowLayout;
	text -l "Line of Actions List";
	setParent ..;


	rowLayout
		-numberOfColumns 2;

	textScrollList list_loa;

	columnLayout;
	button -l "Add LOA" -command "AddSelectedLOA" btn_add_loa;
	button -l "Remove LOA" -command "RemoveSelectedLOA" btn_remove_loa;
	button -l "Clean" -command "CleanLOAList" btn_clean_loa;

	button -l "Deform LOA" -command "DeformToLOA" btn_deform_loa;

	setParent ..;
	setParent ..;


	rowLayout 
		-numberOfColumns 6;

	button -l "Apply" -command "ApplyAnimation" btn_apply;

	button -l "IKNodeTest" -command "ApplyAnimationCpp" btn_ik_node_test;
	
	button -l "Cancel" btn_cancel;

	setParent ..;


	//the animation curve editor
	tearOffPanel "Graph Editor" "graphEditor" true;

	showWindow animationSketcherWindow;
}


global string $nameMenu;

global proc createMenu()
{
    global string $nameMenu;
    $nameMenu=`menu -label "AnimationSketcher" -parent "MayaWindow"`;

	menuItem -label "AnimationSketcher Menu" -command "AnimationSketcherMainMenu()";
}

global proc deleteMenu()
{
    global string $nameMenu;
    deleteUI -m $nameMenu;
}



