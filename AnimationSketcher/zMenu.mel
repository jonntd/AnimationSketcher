
global proc BindSelected3dCharacter()
{
	// currently simply bind a geometry
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		
		textField -e -text $selected[0] tf_3dc;
    }
}

global proc BindSelectTrajectory()
{
	// bind a NURB curve (projected?)
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		
        textField -e -text $selected[0] tf_trajectory;
    }
}

global proc BindSelectedJoint()
{
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		
        textField -e -text $selected[0] tf_rootJoint;
    }
}

global proc BindSelectedJointEnd()
{
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
        textField -e -text $selected[0] tf_endJoint;
    }
}

//----- Bind LOA ----------

// return: curveIntersect Node
global proc string GetIntersectNode(string $c1, string $c2)
{
	//TODO: use direction, active view direction
	string $node = `curveIntersect -ch on $c1 $c2`;
	return $node;
}


//---------------------------
// Self collision detection
//--------------------------

global proc float[] GetSelfIntersection(string $c)
{
	int $spans = getAttr ($c+".spans");
	int $i;

	float $p[];		// self intersecting point

	for($i=1; $i < $spans; ++$i)
	{
		string $dc[] = `detachCurve -ch 1 -cos on ($c + ".u[" + $i + "]")`;
		string $dc2[] = `detachCurve -ch 1 -cos on -rpo 1 ($dc[0] + ".u[" + ($i-0.01) + "]")`;

		// curveIntersect
		string $tmp = `curveIntersect $dc2[1] $dc[1]`;
		
		//print $dc2[0];
		//print $dc[1];

		if($tmp != "")
		{
			string $split_tmp[];
			tokenize ($tmp," ",$split_tmp);
			float $intersect_p1 = $split_tmp[0];
			float $intersect_p2 = $split_tmp[1];
			$p[size($p)] = $intersect_p1;
			$p[size($p)] = $intersect_p2;

			//print $tmp;
			//print $intersect_p;
		}

		
		select -r $dc2[0];
		doDelete;
		select -r $dc2[1];
		doDelete;
		select -r $dc[1];
		doDelete;
		
	}


	return $p;

}







//use `pointOnCurve` to get position


// LOA List

global proc AddSelectedLOA()
{
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		textScrollList -e -append $selected[0] -sc "OnSelectLOAListItem" list_loa;
    }
}

global proc RemoveSelectedLOA()
{
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		textScrollList -e -removeItem $selected[0] list_loa;
    }
}

global proc CleanLOAList()
{
	textScrollList -e -ra list_loa;
}


global proc OnSelectLOAListItem()
{
	//string $stextScrollList
	select -r `textScrollList -q -selectItem list_loa`;
}

//----------------------------



// deform to currently selected LOA

global proc DeformToLOA_Pure()
{
	string $root = `textField -query -text tf_rootJoint`;
	string $end = `textField -query -text tf_endJoint`;

	string $childrenJoints[] = `listRelatives -ad $root`;
	string $end = $childrenJoints[0];

	CurveIKCmd -root $root -end $end;
}

global proc DeformToLOA()
{
	select -r `textScrollList -q -selectItem list_loa`;

	DeformToLOA_Pure();
}




//-----------------------------


// return ik handle name
global proc string ApplyAnimation_FlowMotionPath()
{
	string $character = `textField -query -text tf_3dc`;
	string $curveName = `textField -query -text tf_trajectory`;
    string $rootJointName = `textField -query -text tf_rootJoint`;
    
    string $childrenJoints[] = `listRelatives -ad $rootJointName`;
    string $leafJointName = $childrenJoints[0];
    select -r $rootJointName $leafJointName;
    string $ikHandlesNames[] = `ikHandle -sol ikSplineSolver -ns 30`;
    string $ikHandleName = $ikHandlesNames[0];
    string $ikSplineName = $ikHandlesNames[2];

    xform -cp $ikSplineName; //Center spline curve pivot

    select -r $ikHandleName;
    setAttr ($ikHandleName+".dTwistControlEnable") 1;

    //pathAnimation -startTimeU `playbackOptions -query -minTime` -endTimeU `playbackOptions -query -maxTime` -c $curveName $character;
    select -r $ikSplineName $curveName;
    
	//pathAnimation -fractionMode true -follow true -followAxis z -upAxis y -worldUpType "vector" -worldUpVector 0 1 0 -inverseUp false -inverseFront false -bank false -startTimeU `playbackOptions -query -minTime` -endTimeU  `playbackOptions -query -maxTime`;
	pathAnimation -fractionMode true -follow true -followAxis x -upAxis y -worldUpType "vector" -worldUpVector 0 1 0 -inverseUp false -inverseFront false -bank false -startTimeU `playbackOptions -query -minTime` -endTimeU  `playbackOptions -query -maxTime`;

    //flow -divisions 2 2 30 -objectCentered 1 -localCompute 0 -localDivisions 2 2 2 $ikSplineName;
	flow -divisions 30 2 2 -objectCentered 1 -localCompute 0 -localDivisions 2 2 2 $ikSplineName;

	return $ikHandleName;
}

// return ik handle name
global proc string ApplyAnimation_TranslateOnly()
{
	string $character = `textField -query -text tf_3dc`;
	string $curveName = `textField -query -text tf_trajectory`;
    string $rootJointName = `textField -query -text tf_rootJoint`;
    
    string $childrenJoints[] = `listRelatives -ad $rootJointName`;
    string $leafJointName = $childrenJoints[0];
    select -r $rootJointName $leafJointName;
    string $ikHandlesNames[] = `ikHandle -sol ikSplineSolver -ns 30`;
    string $ikHandleName = $ikHandlesNames[0];
    string $ikSplineName = $ikHandlesNames[2];

    xform -cp $ikSplineName; //Center spline curve pivot

    select -r $ikHandleName;
    setAttr ($ikHandleName+".dTwistControlEnable") 1;

    select -r $ikSplineName $curveName;
    
	pathAnimation -follow false -fractionMode true -upAxis x -worldUpType "vector" -worldUpVector 0 1 0 -inverseUp false -inverseFront false -bank false -startTimeU `playbackOptions -query -minTime` -endTimeU  `playbackOptions -query -maxTime`;

	return $ikHandleName;
}

global proc ApplyAnimationCpp()
{
    string $character = `textField -query -text tf_3dc`;
	string $curveName = `textField -query -text tf_trajectory`;
    string $rootJointName = `textField -query -text tf_rootJoint`;

    string $childrenJoints[] = `listRelatives -ad $rootJointName`;
    string $leafJointName = $childrenJoints[0];
    select -r $rootJointName $leafJointName;
    createNode ikSolverNode;
    ikHandle -sol ikSolverNode -sj $rootJointName -ee $leafJointName;
}


global proc ApplyMotionPathNode()
{
    string $character = `textField -query -text tf_3dc`;
	string $curveName = `textField -query -text tf_trajectory`;
    
    // Create an anim curve to define how the motion progresses along
    // the path. Here we assume the animation is 114 frames long and
    // progresses linearly in time. (Add UI to allow modifications the anim curve?)
    string $ac = `createNode animCurveTU -n "animCurve"`;
    setKeyframe -t 1 -v 0 $ac;
    setKeyframe -t 114 -v 1 $ac;

    // Create a motionPathNode and connect the path and anim curve.
    //string $mp = `createNode motionPathNode`;
	string $mp = `createNode motionPath`;
    setAttr ($mp+".fractionMode") true;
    connectAttr ($ac+".output") ($mp+".uValue");
    connectAttr ($curveName+".worldSpace[0]") ($mp+".geometryPath");

    connectAttr ($mp+".allCoordinates") ($character+".translate");
    connectAttr ($mp+".rotate") ($character+".rotate");
    
    // Add rotation so the character follows the path:
    // 1) Turn on the "follow" attribute to enable rotation.
    setAttr ($mp+".follow") 1;
}



global proc float[] GetRollingParam(string $curveName)
{

	float $loop_p_all[] = GetSelfIntersection($curveName);
	// (p1,p2) - one intersecting point

	float $loop_p[];
	float $current_min1 = 0;
	float $current_min2 = 0;

	int $i;
	int $len = size($loop_p_all);
	for($i=0; $i<$len; $i+=2)
	{
		float $p1 = $loop_p_all[$i];
		float $p2 = $loop_p_all[($i+1)];
		if($p1 > $current_min1)
		{
			$current_min1 = $p1 + 0.01;
			$current_min2 = $p2 + 0.01;

			$loop_p[size($loop_p)] = $p1;
			$loop_p[size($loop_p)] = $p2;

			//$loop_p[size($loop_p)] = ($p1 + $p2) / 2;
		}
	}

	//now loop self intersecting points are in $loop_p
	return $loop_p;
}








// Final version of apply animation
// 1. create motion path follow
// 2. for loa : loa list
//		find intersect point, param
//		deform to loa
//		insert keyframe (set ik fk control and blend weight)
//		


global proc ApplyAnimation()
{
	//get all joints
	string $rootJointName = `textField -query -text tf_rootJoint`;
	string $childrenJoints[] = `listRelatives -ad $rootJointName`;

	string $curveName = `textField -query -text tf_trajectory`;
	
	$childrenJoints[size($childrenJoints)] = $rootJointName;		//add root to the list

	/*
	for($j in $childrenJoints)
	{
		setAttr ($j+".jointOrientX", 0);
		setAttr ($j+".jointOrientY", 0);
		setAttr ($j+".jointOrientZ", 0);
	}
	*/

	//print $childrenJoints[0];
	//print $childrenJoints[size($childrenJoints)-1];
	
	

	//limitJointRotation($childrenJoints);

	// trajectory flow path
	string $ikhandle = ApplyAnimation_FlowMotionPath();
	setAttr ($ikhandle + ".ikFkManipulation", 1);

	// iterate list
	string $loa_array[] = `textScrollList -q -allItems list_loa`;
	string $trajectory = `textField -query -text tf_trajectory`;
	float $spans = `getAttr ($trajectory + ".spans")`;

	

	
	
	//TODO: set total_frames
	int $blend_frames = `floatSliderGrp -q -v sld_blend`;
	int $total_frames = `floatSliderGrp -q -v sld_total`;



	//int $center_frame_array[];

	


	for ($loa in $loa_array)
	{
		string $intersect_node = GetIntersectNode($trajectory, $loa);
		float $u[] = `getAttr ($intersect_node + ".p1")`;

		float $p = $u[0] / $spans;		// 0 - 1

		//print $u;

		int $center_frame = $p * $total_frames;
		//$center_frame_array[size($center_frame_array)] = $center_frame;
		int $blend_in_frame = $center_frame - $blend_frames;
		if($blend_in_frame <= 0) 
		{
			$blend_in_frame = 1;
		}
		int $blend_out_frame = $center_frame + $blend_frames;

		//insert key frames for joints transform
		currentTime $blend_in_frame ;
		setAttr ($ikhandle + ".ikBlend", 1);
		setKeyframe { ($ikhandle + ".ikb") };

		//unlimitJointRotation($childrenJoints);
		

		for($j in $childrenJoints)
		{
			//setKeyframe ($j + ".rx");
			//setKeyframe ($j + ".ry");
			//setKeyframe ($j + ".rz");

			//setKeyframe ($j + ".tx");
			//setKeyframe ($j + ".tz");

			setKeyframe {($j)};
		}

		
		currentTime $center_frame ;
		setAttr ($ikhandle + ".ikBlend", 0);
		setKeyframe { ($ikhandle + ".ikb") };



		currentTime $blend_out_frame ;
		setAttr ($ikhandle + ".ikBlend", 1);
		setKeyframe { ($ikhandle + ".ikb") };

		//unlimitJointRotation($childrenJoints);

		for($j in $childrenJoints)
		{
			//setKeyframe ($j + ".rx");
			//setKeyframe ($j + ".ry");
			//setKeyframe ($j + ".rz");

			//setKeyframe ($j + ".tx");
			//setKeyframe ($j + ".tz");

			setKeyframe {($j)};
		}


		//insert key frames for ikhandle weights

		currentTime ($blend_in_frame + 1) ;
		setAttr ($ikhandle + ".ikBlend", 0);
		setKeyframe { ($ikhandle + ".ikb") };

		//limitJointRotation($childrenJoints);
		
		currentTime ($blend_out_frame - 1) ;
		setAttr ($ikhandle + ".ikBlend", 0);
		setKeyframe { ($ikhandle + ".ikb") };
		
		//limitJointRotation($childrenJoints);
		

		// fk deform to loa

		currentTime $center_frame;
		select -r $loa;
		DeformToLOA_Pure();

		// set keyframe for each joints
		for($j in $childrenJoints)
		{
			//setKeyframe ($j + ".rx");
			//setKeyframe ($j + ".ry");
			//setKeyframe ($j + ".rz");

			//setKeyframe ($j + ".tx");
			//setKeyframe ($j + ".tz");

			setKeyframe {($j)};
		}
	}



	
	//Rolling generate
	float $loop_p[] = GetRollingParam($curveName);
	
	int $i;
	int $len = size($loop_p);
	for($i=0; $i<$len; $i+=2)
	{
		float $p_in = $loop_p[$i];
		float $p_out = $loop_p[$i+1];
		float $p = ($p_in + $p_out) / 2 + 0.5; 


		int $center_frame = $p / $spans * $total_frames;


		//int $blend_in_frame = $center_frame - $blend_frames;
		//int $blend_in_frame = $p_in / $spans * $total_frames;
		int $blend_in_frame = ($p_in+0.5) / $spans * $total_frames;


		if($blend_in_frame <= 0) 
		{
			$blend_in_frame = 1;
		}
		//int $blend_out_frame = $center_frame + $blend_frames;
		//int $blend_out_frame = $p_out / $spans * $total_frames;
		int $blend_out_frame = ($p_out+0.5) / $spans * $total_frames;


		//insert key frames for joints transform
		currentTime $blend_in_frame ;
		setAttr ($ikhandle + ".ikBlend", 1);
		setKeyframe { ($ikhandle + ".ikb") };

		float $in_x = `getAttr ($rootJointName+".translateX")`;

		for($j in $childrenJoints)
		{

			setKeyframe {($j)};
		}

		
		currentTime $center_frame ;
		setAttr ($ikhandle + ".ikBlend", 0);
		setKeyframe { ($ikhandle + ".ikb") };

		currentTime $blend_out_frame ;
		setAttr ($ikhandle + ".ikBlend", 1);
		setKeyframe { ($ikhandle + ".ikb") };

		float $out_x = `getAttr ($rootJointName+".translateX")`;

		float $root_rz = `getAttr ($rootJointName + ".rotateZ")`;
		setAttr ($rootJointName + ".rotateZ") ($root_rz - 360);
		

		for($j in $childrenJoints)
		{
			setKeyframe {($j)};
		}


		//insert key frames for ikhandle weights

		currentTime ($blend_in_frame + 1) ;
		setAttr ($ikhandle + ".ikBlend", 0);
		setKeyframe { ($ikhandle + ".ikb") };
		
		currentTime ($blend_out_frame - 1) ;
		setAttr ($ikhandle + ".ikBlend", 0);
		setKeyframe { ($ikhandle + ".ikb") };

		// fk deform to loa

		currentTime $center_frame;

		float $intersect_position[] = `pointOnCurve -pr $p_in -p $curveName`;
		//setAttr ($rootJointName + ".translateX") (($in_x + $out_x) / 2);
		setAttr ($rootJointName + ".translateX") ($intersect_position[0]);
		setKeyframe {($rootJointName)};

		//print $p;
		//print "\n";
		//print $center_frame;
	}
	
}







// ----------- Main menu ---------------------

global proc AnimationSketcherMainMenu()
{
	if (`window -exists animationSketcherWindow`) deleteUI animationSketcherWindow;

	window -title "Animation Sketcher" -widthHeight 600 480 animationSketcherWindow;

	columnLayout -adj 1;

	rowLayout 
		-numberOfColumns 3 
		-columnWidth3 150 300 150 
		-columnAlign3 "right" "center" "center";

	text -l "3D Character" t_3dc;
	textField -ed false -w 300 tf_3dc;
	button -l "Bind Selected Character" -command "BindSelected3dCharacter" btn_3dc;

	setParent ..;

	rowLayout 
		-numberOfColumns 3 
		-columnWidth3 150 300 150 
		-columnAlign3 "right" "center" "center";

	text -l "Trajectory Curve" t_trajectory;
	textField -ed false -w 300 tf_trajectory;
	button -l "Bind Selected Curve" -command "BindSelectTrajectory" btn_trajectory;

	setParent ..;

	rowLayout 
		-numberOfColumns 3 
		-columnWidth3 150 300 150 
		-columnAlign3 "right" "center" "center";

	text -l "Shape Root Joint" t_rootJoint;
	textField -ed false -w 300 tf_rootJoint;
	button -l "Bind Selected Joint" -command "BindSelectedJoint" btn_rootJoint;

	setParent ..;

	rowLayout 
		-numberOfColumns 3 
		-columnWidth3 150 300 150 
		-columnAlign3 "right" "center" "center";

	text -l "Shape End effector Joint" t_endJoint;
	textField -ed false -w 300 tf_endJoint;
	button -l "Bind Selected Joint" -command "BindSelectedJointEnd" btn_endJoint;

	setParent ..;

	rowLayout;
	floatSliderGrp -label "loa blend frame" -field true -value 10 -min 1 -max 10 -fieldMinValue 1 -fieldMaxValue 100 sld_blend;
	setParent ..;

	rowLayout;
	floatSliderGrp -label "total animation frames" -field true -value 120 -min 1 -max 120 -fieldMinValue 1 -fieldMaxValue 500 sld_total;
	setParent ..;


	//----- LOA menu list
	rowLayout;
	text -l "Line of Actions List";
	setParent ..;


	rowLayout
		-numberOfColumns 2;

	textScrollList list_loa;

	columnLayout;
	button -l "Add LOA" -command "AddSelectedLOA" btn_add_loa;
	button -l "Remove LOA" -command "RemoveSelectedLOA" btn_remove_loa;
	button -l "Clean" -command "CleanLOAList" btn_clean_loa;

	button -l "Deform LOA" -command "DeformToLOA" btn_deform_loa;

	setParent ..;
	setParent ..;


	rowLayout 
		-numberOfColumns 6;

	button -l "Apply" -command "ApplyAnimation" btn_apply;
	//button -l "Apply" -command "ApplyAnimation_FlowMotionPath" btn_apply;
	
	button -l "Apply_Bounce" -command "ApplyAnimation_TranslateOnly" btn_apply2;

	button -l "IKNodeTest" -command "ApplyAnimationCpp" btn_ik_node_test;
	
	button -l "Cancel" btn_cancel;

	setParent ..;


	//the animation curve editor
	tearOffPanel "Graph Editor" "graphEditor" true;

	showWindow animationSketcherWindow;
}


global string $nameMenu;

global proc createMenu()
{
    global string $nameMenu;
    $nameMenu=`menu -label "AnimationSketcher" -parent "MayaWindow"`;

	menuItem -label "AnimationSketcher Menu" -command "AnimationSketcherMainMenu()";
}

global proc deleteMenu()
{
    global string $nameMenu;
    deleteUI -m $nameMenu;
}



