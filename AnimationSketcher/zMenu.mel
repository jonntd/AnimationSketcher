
global proc BindSelected3dCharacter()
{
	// currently simply bind a geometry
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		
		textField -e -text $selected[0] tf_3dc;
    }
}

global proc BindSelectTrajectory()
{
	// bind a NURB curve (projected?)
	string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
		
        textField -e -text $selected[0] tf_trajectory;
    }
}


global proc ApplyAnimation()
{
	string $character = `textField -query -text tf_3dc`;
	string $curveName = `textField -query -text tf_trajectory`;
	pathAnimation -startTimeU `playbackOptions -query -minTime` -endTimeU `playbackOptions -query -maxTime` -c $curveName $character;
}


global proc AnimationSketcherMainMenu()
{
	if (`window -exists animationSketcherWindow`) deleteUI animationSketcherWindow;

	window -title "Animation Sketcher" -widthHeight 600 480 animationSketcherWindow;

	columnLayout -adj 1;

	rowLayout 
		-numberOfColumns 3 
		-columnWidth3 150 300 150 
		-columnAlign3 "right" "center" "center";

	text -l "3D Character" t_3dc;
	textField -ed false -w 300 tf_3dc;
	button -l "Bind Selected Character" -command "BindSelected3dCharacter" btn_3dc;

	setParent ..;

	rowLayout 
		-numberOfColumns 3 
		-columnWidth3 150 300 150 
		-columnAlign3 "right" "center" "center";

	text -l "Trajectory" t_trajectory;
	textField -ed false -w 300 tf_trajectory;
	button -l "Bind Selected Curve" -command "BindSelectTrajectory" btn_trajectory;

	setParent ..;

	rowLayout 
		-numberOfColumns 3 
		-columnWidth3 150 300 150 
		-columnAlign3 "right" "center" "center";

	//TODO: this should be a table, a list of curves
	text -l "Shape matching sketch" t_shape;
	textField -ed false -w 300 tf_shape;
	button -l "Add Selection" -command "AddShapeMatchingSketch" btn_shape;

	setParent ..;


	rowLayout 
		-numberOfColumns 6;

	button -l "Apply" -command "ApplyAnimation" btn_apply;
	button -l "Cancel" btn_cancel;

	setParent ..;

	//the animation curve editor
	tearOffPanel "Graph Editor" "graphEditor" true;






	showWindow animationSketcherWindow;
}


global proc RandomNodeDefault()
{
    string $cube[] = `polyCube`;
    string $inst1 = `instancer`;
    string $randnode = `createNode randomNode`;
    connectAttr ($cube[0]+".matrix", $inst1+".inputHierarchy[0]");
    connectAttr ($randnode+".outPoints", $inst1+".inputPoints");
}

global proc RandomNodeSelected()
{
    string $selected[] = `ls -sl`;
    if(size($selected) > 0)
    {
        string $inst1 = `instancer`;
        string $randnode = `createNode randomNode`;
        connectAttr ($selected[0]+".matrix", $inst1+".inputHierarchy[0]");
        connectAttr ($randnode+".outPoints", $inst1+".inputPoints");
    }
    else
    {
        print "Random Node: Nothing selected!";
    }
}

global proc LSystemNodeDefault()
{
    string $cube[] = `polyCube`;
    string $sphere[] = `polySphere`;
    string $inst1 = `instancer`;
    string $inst2 = `instancer`;
    string $lsnode = `createNode LSystemInstanceNode`;
    connectAttr ($cube[0]+".matrix",$inst1+".inputHierarchy[0]");
    connectAttr ($sphere[0]+".matrix", $inst2+".inputHierarchy[0]");
    connectAttr ($lsnode+".outBranches", $inst1+".inputPoints");
    connectAttr ($lsnode+".outFlowers", $inst2+".inputPoints");
}

global proc LSystemNodeSelected()
{
    string $selected[] = `ls -sl`;
    if(size($selected) >= 2)
    {
        string $inst1 = `instancer`;
        string $inst2 = `instancer`;
        string $lsnode = `createNode LSystemInstanceNode`;
        connectAttr ($selected[0]+".matrix",$inst1+".inputHierarchy[0]");
        connectAttr ($selected[1]+".matrix", $inst2+".inputHierarchy[0]");
        connectAttr ($lsnode+".outBranches", $inst1+".inputPoints");
        connectAttr ($lsnode+".outFlowers", $inst2+".inputPoints");
    }
    else
    {
        print "LSystem Node: Not enough geometries selected!";
    }
}

global string $nameMenu;

global proc createMenu()
{
    global string $nameMenu;
    $nameMenu=`menu -label "AnimationSketcher" -parent "MayaWindow"`;
    menuItem -label "Random Node Default" -command "RandomNodeDefault()";
    menuItem -label "Random Node Selected" -command "RandomNodeSelected()";
    menuItem -label "LSystem Node Default" -command "LSystemNodeDefault()";
    menuItem -label "LSystem Node Selected" -command "LSystemNodeSelected()";
	menuItem -label "AnimationSketcher Menu" -command "AnimationSketcherMainMenu()";
}

global proc deleteMenu()
{
    global string $nameMenu;
    deleteUI -m $nameMenu;
}



